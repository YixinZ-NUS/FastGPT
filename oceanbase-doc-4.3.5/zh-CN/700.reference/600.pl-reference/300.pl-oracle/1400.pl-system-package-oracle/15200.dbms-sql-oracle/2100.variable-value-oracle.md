| Description   |                 |
|---------------|-----------------|
| keywords      |                 |
| dir-name      |                 |
| dir-name-en   |                 |
| tenant-type   | Oracle Mode     |
|machine-translation||

# VARIABLE_VALUE

`VARIABLE_VALUE` 存储过程用于在执行动态 SQL 语句后，获取输出变量的值。

## 语法

```sql
DBMS_SQL.VARIABLE_VALUE(
  cursor      IN  NUMBER,
  variable    IN  VARCHAR2,
  value       OUT value_type
);
```

## 参数解释

|    **参数**     |           **解释**            |
|---------------|-----------------------------|
|cursor|已打开的动态 SQL 游标 ID。|
|variable|需要获取的变量名称。|
|value|存储返回值的目标变量。|

## 使用示例

执行如下命令，创建了一个包含四列的表 `tab`，并通过存储过程 `single_Row_insert` 使用动态 SQL 插入数据到表中，同时通过 `RETURNING` 子句将计算值 `（c1*c2）` 和插入的日期值返回给输出参数，并最终输出结果。

```sql
create table tab(c1 number, c2 number, c3 date, c4 varchar2(10));

delimiter /;
CREATE OR REPLACE PROCEDURE single_Row_insert
           (c1 NUMBER, c2 NUMBER, c3 date, c4 varchar2, r1 OUT NUMBER, r2 OUT date, r3 OUT varchar2) is
c NUMBER;
n NUMBER;
begin
  c := DBMS_SQL.OPEN_CURSOR;
  DBMS_SQL.PARSE(c, 'INSERT INTO tab VALUES (:bnd1, :bnd2, :bnd3, :bnd4) ' ||
                    'RETURNING c1*c2, c3 INTO :bnd5, :bnd6', DBMS_SQL.NATIVE);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd1', c1);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd2', c2);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd3', c3);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd4', c4);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd5', r1);
  DBMS_SQL.BIND_VARIABLE(c, 'bnd6', r2);
  n := DBMS_SQL.EXECUTE(c);
  DBMS_SQL.VARIABLE_VALUE(c, 'bnd5', r1);-- get value of outbind variable
  DBMS_SQL.VARIABLE_VALUE(c, 'bnd6', r2);-- get value of outbind variable
  DBMS_SQL.CLOSE_CURSOR(c);
END;
/

DECLARE
  a number;
  b date;
  c varchar2(10);
BEGIN
  single_Row_insert(2, 4, to_date('2020-01-01', 'yyyy-mm-dd'), 'Tomorrow', a, b, c);
  dbms_output.put_line(a || ' --- ' || b || ' --- ' || c);
end;
/
```
